#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
    Возврат;
КонецЕсли;

	Сумма = ТоварыИУслуги.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Ответственный = ПараметрыСеанса.СотрудникТекущегоПользователя;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПоступлениеТоваровИУслугТоварыИУслуги.Номенклатура.Тип КАК НоменклатураТип,
	|	ПоступлениеТоваровИУслугТоварыИУслуги.Номенклатура КАК Номенклатура,
	|	ПоступлениеТоваровИУслугТоварыИУслуги.Количество КАК Количество,
	|	ПоступлениеТоваровИУслугТоварыИУслуги.Сумма КАК Сумма
	|ИЗ
	|	Документ.ПоступлениеТоваровИУслуг.ТоварыИУслуги КАК ПоступлениеТоваровИУслугТоварыИУслуги
	|ГДЕ
	|	ПоступлениеТоваровИУслугТоварыИУслуги.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Движения.Товары.Записывать = Истина;
	Движения.Расходы.Записывать = Истина;
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.НоменклатураТип = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар") Тогда
			Движение = Движения.Товары.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Период = Дата;
			Движение.Номенклатура = Выборка.Номенклатура;
			Движение.Количество = Выборка.Количество;
			Движение.Сумма = Выборка.Сумма;
			
		ИначеЕсли Выборка.НоменклатураТип = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Услуга") Тогда
			Движение = Движения.Расходы.Добавить();
			Движение.Период = Дата;
			Движение.Номенклатура = Выборка.Номенклатура;
			Движение.Сумма = Выборка.Сумма;
		КонецЕсли;
	КонецЦикла;
	
	Движения.ВзаиморасчетыСКонтрагентами.Записывать = Истина;
	
	Движение = Движения.ВзаиморасчетыСКонтрагентами.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
	Движение.Период = Дата;
	Движение.Контрагент = Поставщик;
	Движение.Сумма = Сумма;
	
КонецПроцедуры

#КонецОбласти
